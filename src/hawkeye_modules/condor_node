#! /usr/bin/perl -w
use strict;

# Update the module include path
BEGIN
{
    my $Dir = $0;
    if ( $Dir =~ /(.*)\/.*/ )
    {
	push @INC, "$1";
    }
}
use HawkeyePublish;
use HawkeyeLib;

# Setup the hawkeye stuff
my $Hawkeye;

# Do it
Init();
RunIt();

sub Init {
    $Hawkeye = HawkeyePublish->new;
    $Hawkeye->Quiet( 1 );
    $Hawkeye->AutoIndexSet( 1 );
}

# Do the real work here...
sub RunIt {

    # Data we'll gather...
    my %CondorInfo = (
		      "NumStarters"	=> 0,
		      "NumStartds"	=> 0,
		      "NumSchedds"	=> 0,
		      "NumMasters"	=> 0,
		      "NumExecs"	=> 0,
		      "NumShadows"	=> 0,
		      "NumRunaway"	=> 0,
		     );
    # Table to translate "ps-command" regex to $CondorInfo key..
    my %CondorCmdTable = (
			  "condor_starter"	=> "NumStarters",
			  "condor_startd"	=> "NumStartds",
			  "condor_master"	=> "NumMasters",
			  "condor_exec"		=> "NumExecs",
			  "condor_shadow"	=> "NumShadows",
			  "condor_schedd"	=> "NumSchedds",
			 );

    # Run a 'ps' and gather some info...
    if ( open( PS, "/bin/ps -eo user,pid,ppid,command|" ) )
    {
	while ( <PS> )
	{
	    chomp;
	    my ( $User, $Pid, $Ppid, @CmdString ) = split;
	    my $CmdString = join(" ", @CmdString);
	    next if ( ! ( $CmdString =~ /condor/ ) );

	    # Starter with PPID == 1
	    if ( 1 == $Ppid )
	    {
		if ( ( $CmdString =~ /condor_starter/ ) ||
		     ( $CmdString =~ /condor_exec/ ) )
		{
		    $CondorInfo{NumRunaway}++;
		}
	    }

	    # Count the processes...
	    foreach my $Match ( keys %CondorCmdTable )
	    {
		if ( $CmdString =~ /$Match/ )
		{
		    my $Label = $CondorCmdTable{$Match};
		    if ( ! exists $CondorInfo{$Label} )
		    {
			$CondorInfo{$Label} = 1;
		    }
		    else
		    {
			$CondorInfo{$Label}++;
		    }
		}
	    }
	}
	close( PS );
    }

    # Now, run condor_status and gather some more info...
    if ( $CondorInfo{NumStartds} )
    {
	if ( open( CSTAT, "condor_status -direct $ENV{HOST} |" ) )
	{
	    my $ShortHost = substr( $ENV{HOST}, 0, 14 );
	    while ( <CSTAT> )
	    {
		chomp;

		# Normal "machine" line
		next if ( ! /^$ShortHost/ );

		my ( $Host, $OpSys, $Arch, $State, $Act, $Load, $Mem, $Time )
		    = split;
		$CondorInfo{OpSys} = $OpSys;
		$CondorInfo{Arch} = $Arch;
		$CondorInfo{State} = $State;
		$CondorInfo{Activity} = $Act;
		$CondorInfo{LoadAv} = $Load;
		$CondorInfo{Mem} = $Mem;
		if ( $Time =~ /(\d+)+(\d+):(\d+):(\d+)/ )
		{
		    $CondorInfo{ActivityTime} = ( ( $4 ) +
						  ( $3 * 60) +
						  ( $2 * 60 * 60 ) +
						  ( $1 * 60 * 60 * 24 ) );
		}
	    }
	}
	close( CSTAT );
    }

    # Publish 'em all
    foreach my $Key ( keys %CondorInfo )
    {
	if ( $CondorInfo{$Key} =~ /[a-zA-Z]/ )
	{
	    $Hawkeye->Store( $Key, $CondorInfo{$Key} );
	}
	else
	{
	    $Hawkeye->StoreNum( $Key, $CondorInfo{$Key} );
	}
    }

    # Let 'er rip!
    $Hawkeye->Publish( );
}
