#! /usr/bin/perl -w
use strict;

# Update the module include path
BEGIN
{
    my $Dir = $0;
    if ( $Dir =~ /(.*)\/.*/ )
    {
	push @INC, "$1";
    }
}
use HawkeyePublish;
use HawkeyeLib;

# Setup the hawkeye stuff
my $Hawkeye;

# Setup
my $MasterUserList = "";

# Do it
Init();
RunIt();

# Init logic
sub Init {
    HawkeyeLib::DoConfig( );

    $Hawkeye = HawkeyePublish->new;
    $Hawkeye->Quiet( 1 );
    $Hawkeye->AutoIndexSet( 1 );

    my $Tmp = HawkeyeLib::ReadConfig( "_master_owners", "condor,root" );
    $MasterUserList = $Tmp if ( $Tmp ne "" );
}

# Do the real work here...
sub RunIt {

    # Data we'll gather...
    my %CondorInfo = (
		      "NumStarters"	=> 0,
		      "NumStartds"	=> 0,
		      "NumSchedds"	=> 0,
		      "NumMasters"	=> 0,
		      "NumExecs"	=> 0,
		      "NumShadows"	=> 0,
		      "NumRunaway"	=> 0,
		      "Vms"		=> "",
		     );

    # Table to translate "ps-command" regex to $CondorInfo key..
    my %CondorCmdTable =
	(
	 condor_starter	=> { Attr => "NumStarters", UserList => "", },
	 condor_startd	=> { Attr => "NumStartds", UserList => "", },
	 condor_master	=> { Attr => "NumMasters", UserList => "condor,root", },
	 condor_exec	=> { Attr => "NumExecs", UserList => "", },
	 condor_shadow	=> { Attr => "NumShadows", UserList => "", },
	 condor_schedd	=> { Attr => "NumSchedds", UserList => "", },
	);

    $CondorCmdTable{condor_master}{UserList} = $MasterUserList
	if ( $MasterUserList ne "" );

    # Run a 'ps' and gather some info...
    if ( open( PS, "/bin/ps -eo user,pid,ppid,command|" ) )
    {
	while ( <PS> )
	{
	    chomp;
	    my ( $User, $Pid, $Ppid, @CmdString ) = split;
	    my $CmdString = join(" ", @CmdString);
	    next if ( ! ( $CmdString =~ /condor/ ) );

	    # Starter with PPID == 1
	    if ( 1 == $Ppid )
	    {
		if ( ( $CmdString =~ /condor_starter/ ) ||
		     ( $CmdString =~ /condor_exec/ ) )
		{
		    $CondorInfo{NumRunaway}++;
		}
	    }

	    # Count the processes...
	    foreach my $CmdPat ( keys %CondorCmdTable )
	    {
		if ( $CmdString =~ /$CmdPat/ )
		{
		    my $Attr = $CondorCmdTable{$CmdPat}->{Attr};
		    my $UserList = $CondorCmdTable{$CmdPat}->{UserList};

		    # Is it on the user match list?
		    my $UserMatch = 0;
		    foreach my $TmpUser ( split( /,/, $UserList ) )
		    {
			$UserMatch++ if (  ( $TmpUser eq "" ) ||
					   ( $TmpUser eq $User )  );
		    }
		    last if ( ! $UserMatch );

		    # Update the count...
		    if ( ! exists $CondorInfo{$Attr} )
		    {
			$CondorInfo{$Attr} = 1;
		    }
		    else
		    {
			$CondorInfo{$Attr}++;
		    }
		}
	    }
	}
	close( PS );
    }

    # Now, run condor_status and gather some more info...
    if ( $CondorInfo{NumStartds} )
    {
	if ( open( CSTAT, "condor_status -direct $ENV{HOST} |" ) )
	{
	    while ( <CSTAT> )
	    {
		chomp;

		# Skip the 'uniteresting' lines
		next if ( /^Name\s+OpSys/ );
		next if ( /^\s/ );
		next if ( $_ eq "" );

		my $Vm = "";
		my ( $Host, $OpSys, $Arch, $State, $Act, $Load, $Mem, $Time )
		    = split;
		if ( $Host =~ /(.*)@(.*)/ )
		{
		    $Vm = "_$1";
		    $CondorInfo{Vms} = $CondorInfo{Vms} . "$1 ";
		}
		$CondorInfo{OpSys} = $OpSys;
		$CondorInfo{Arch} = $Arch;
		$CondorInfo{"State$Vm"} = $State;
		$CondorInfo{"Activity$Vm"} = $Act;
		$CondorInfo{"LoadAv$Vm"} = $Load;
		$CondorInfo{"Mem$Vm"} = $Mem;
		if ( $Time =~ /(\d+)+(\d+):(\d+):(\d+)/ )
		{
		    $CondorInfo{"ActivityTime$Vm"} =
			( ( $4 ) +
			  ( $3 * 60) +
			  ( $2 * 60 * 60 ) +
			  ( $1 * 60 * 60 * 24 ) );
		}
	    }
	    close( CSTAT );
	}
	else
	{
	    print STDERR "Can't query startd\n";
	}
    }

    # Publish 'em all
    foreach my $Key ( keys %CondorInfo )
    {
	if (  ( $CondorInfo{$Key} =~ /[a-zA-Z]/ ) ||
	      ( $CondorInfo{$Key} eq "" )   )
	{
	    $Hawkeye->Store( $Key, $CondorInfo{$Key} );
	}
	else
	{
	    $Hawkeye->StoreNum( $Key, $CondorInfo{$Key} );
	}
    }

    # Let 'er rip!
    $Hawkeye->Publish( );
}
