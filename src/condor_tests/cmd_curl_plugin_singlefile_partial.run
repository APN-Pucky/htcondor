#! /usr/bin/env perl
##**************************************************************
##
## Copyright (C) 1990-2007, Condor Team, Computer Sciences Department,
## University of Wisconsin-Madison, WI.
## 
## Licensed under the Apache License, Version 2.0 (the "License"); you
## may not use this file except in compliance with the License.  You may
## obtain a copy of the License at
## 
##	http://www.apache.org/licenses/LICENSE-2.0
## 
## Unless required by applicable law or agreed to in writing, software
## distributed under the License is distributed on an "AS IS" BASIS,
## WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
## See the License for the specific language governing permissions and
## limitations under the License.
##
##**************************************************************
##
## This is a very basic test of curl_plugin to show that it works
## corretly in certain success and failure conditions. 
##
##**************************************************************

use Carp;
use CondorTest;
use CondorUtils;
use Check::SimpleJob;
use Check::CondorLog;
use Cwd;
use ConfigTools;
use POSIX;

use warnings;
use strict;

my $childPid;
my $childProcess;
my $localDir = cwd();
my $partialNoRangeSize;
my $partialSize;
my $returnCode;
my $submitfile = "curl-basic-transfer.sub";
my $testname = "cmd_curl_plugin";


# Start the http server then wait a second
startFlakyWebServer();
sleep(1);

# Read in the address of the http server from file
open FILE, "$localDir/flaky-http-address";
my $url = <FILE>;
close FILE;

# Create the submit file
my @basesubmit = "
	universe = vanilla
	executable = cmd_curl_plugin_singlefile_partial.pl
	output = cmd_curl_plugin_singlefile_partial.out
	arguments = 1
	log = $submitfile.log
	transfer_input_files = http://$url/partial
	should_transfer_files = YES
	";
open(SF,">$submitfile") or die "Failed to open '$submitfile' : $!\n";
print SF @basesubmit;
print SF "queue\n";
close(SF);

# Callbacks for the log reader while the job is running.

my $executed = sub
{
	my %args = @_;
	my $cluster = $args{"cluster"};

	TLOG "OK: Job $cluster began executing\n";
	return 0;
};

my $submitted = sub
{
	my %args = @_;
	my $cluster = $args{"cluster"};
	TLOG "OK: Job $cluster was submitted\n";
	return 0;
};

my $exitedSuccess = sub
{
	my %args = @_;
	my $cluster = $args{"cluster"};
	TLOG "OK: Job $cluster exited\n";
	return 0;
};

my $exitedFailure = sub
{
	my %args = @_;
	my $cluster = $args{"cluster"};
	TLOG "ERROR: Job $cluster failed\n";
	cleanupAndExit(0);
};

my $shadowException = sub
{
	my %args = @_;
	my $cluster = $args{"cluster"};
	TLOG "ERROR: Job $cluster failed with a shadow exception\n";
	cleanupAndExit(0);
};

CondorTest::RegisterSubmit($testname, $submitted);
CondorTest::RegisterExecute($testname, $executed);
CondorTest::RegisterExitedSuccess($testname, $exitedSuccess);
CondorTest::RegisterExitedFailure($testname, $exitedFailure);
CondorTest::RegisterShadow($testname, $shadowException);

TLOG("Submitting $submitfile to test a basic curl_plugin transfer\n");

# Run the test. If it fails, one of the handlers will catch. Otherwise it will
# cleanup and exit with a success code (1).
CondorTest::RunTest($testname, $submitfile, 0);


cleanupAndExit(1);


sub cleanupAndExit {
	my $returnCode = shift;
	unlink "$localDir/flaky-http-address";
	unlink "$localDir/$submitfile";
	unlink "$localDir/$submitfile.log";
	unlink "$localDir/$submitfile.log";
	stopFlakyWebServer();
	TLOG "Exiting with return code $returnCode\n";
	CondorTest::RegisterResult($returnCode, test_name=>"cmd_curl_plugin", check_name=>'Basic curl_plugin transfer');
	exit($returnCode);
}

# This sub is based off http://www.perlmonks.org/?node_id=663081
sub startFlakyWebServer {
	# Windows
	if ($^O eq 'MSWin32') { 
		
		require Win32::Process;
		
		# Figure out location of the python binary
		my @myPython = `where python*.exe`;
		if(not(@myPython)) {
			TLOG "Python is required but not available. Expect test failure.\n";
		}
		my $pythonBinary = $myPython[0];
		fullchomp($pythonBinary);

		# Launch the web server process
		Win32::Process::Create($childProcess, $pythonBinary, 
						"$pythonBinary flaky_http_server.py", 0, 0, ".") 
						|| confess "Could not spawn child: $!";
		$childPid = $childProcess->GetProcessID();
	}
	# Unix
	else {
		$SIG{CHLD} = 'IGNORE';
		$childPid = fork();
		unless (defined $childPid) {
			confess "Could not spawn child (Unix): $!";
		}
		if ($childPid == 0 ) { # child
			setsid or warn "setsid cannot start a new session: $!";
			local $| = 1;
			unless (exec("python flaky_http_server.py")) {
				confess "Could not start child: python: $!";
				CORE::exit(0);
			}
		}
		# parent
		$SIG{CHLD} = 'DEFAULT';
	}
	# Catch early child exit, e.g. if program path is incorrect
	sleep(1.0);
	POSIX::waitpid(-1, POSIX::WNOHANG()); # clean up any defunct child process
	if (kill(0, $childPid)) {
		print "Started child process id $childPid\n";
	}
	else {
		warn "Child process exited quickly: python: process $childPid";
	}
}

# This sub is based off http://www.perlmonks.org/?node_id=663081
sub stopFlakyWebServer {
	# Windows
	if ($^O eq 'MSWin32') {
		Win32::Process::KillProcess($childPid,0);
	}
	# Unix
	else {
		kill 9, $childPid || warn "could not kill process $childPid: $!";
	}
	print "Stopped child process id $childPid\n";
}
