#!/bin/csh -f

  set D = ""      # The DAG file
  @ SUBMIT = 1    # Boolean, submit to condor

  switch ($#argv)
    case 1:
      set D = $argv[1]
      breaksw
    case 2:
      if ($argv[1] == "-n") then
        @ SUBMIT = 0
      else
        goto USAGE
      endif
      set D = $argv[2]
      breaksw
    default:
      goto USAGE
  endsw

  set DC = $D.condor

  echo "# Filename: $DC"                       > $DC
  echo "# Generated by condor_submit_dag $D"  >> $DC
  echo "executable   = `which condor_dagman`" >> $DC
  echo "output       = $D.stdout"             >> $DC
  echo "error        = $D.stderr"             >> $DC
  echo "log          = $D.log"                >> $DC
  echo "arguments    = -f -t -l . -Debug 3 -Lockfile $D.lock -Condorlog $D.condor.log -Dag $D" >> $DC
  echo "environment  = PATH=$PATH"            >> $DC
  echo "universe     = scheduler"             >> $DC
  echo "notification = ALWAYS"                >> $DC
  echo "queue"                                >> $DC


  echo "--------------------------------------------------------"
  echo "CondorConfigFile for DAG written to: $DC"
  echo "DAG's stdout             written to: $D.stdout"
  echo "DAG's stderr             written to: $D.stderr"
  echo "DAG's log file           written to: $D.log"
  echo ""
  echo "Condor Log file for all jobs of this DAG: $D.condor.log"
  if ($SUBMIT) then
    condor_submit $DC
  else
    echo "-n given, not submitting DAG to Condor"
  endif
  echo "--------------------------------------------------------"
exit

USAGE:
  echo ""
  echo "Usage: condor_submit_dag  NAME.dag"
  echo "       where NAME is the name your DAG."
  echo ""
exit